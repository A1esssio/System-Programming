{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "ea31edb6",
   "metadata": {},
   "source": [
    "![](./Photo/1.png)\n",
    "![](./Photo/2.png)\n",
    "![](./Photo/3.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f33d19e5",
   "metadata": {},
   "source": [
    "## Assemler code test\n",
    "\n",
    "![](./Photo/4.png)\n",
    "![](./Photo/5.png)\n",
    "![](./Photo/6.png)\n",
    "![](./Photo/7.png)\n",
    "![](./Photo/8.png)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f6e0ac4",
   "metadata": {},
   "source": [
    "## C code test\n",
    "\n",
    "![](./Photo/9.png)\n",
    "![](./Photo/10.png)\n",
    "![](./Photo/11.png)\n",
    "![](./Photo/12.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b73eecdc",
   "metadata": {},
   "source": [
    "## C code\n",
    "\n",
    "```C\n",
    "#include <stdio.h>\n",
    "\n",
    "// 4\n",
    "long long calculate_sum(int n) {\n",
    "    long long sum = 0;\n",
    "    for (int k = 1; k <= n; ++k) {\n",
    "        int sign = (k % 2 == 0) ? 1 : -1;\n",
    "        long long term = (long long)sign * k * (k + 4) * (k + 8);\n",
    "        sum += term;\n",
    "    }\n",
    "    return sum;\n",
    "}\n",
    "\n",
    "// 6\n",
    "int count_numbers(int n) {\n",
    "    int count = 0;\n",
    "    for (int i = 1; i <= n; ++i) {\n",
    "        if (i % 3 != 0 && i % 7 != 0 && i % 5 == 0) {\n",
    "            ++count;\n",
    "        }\n",
    "    }\n",
    "    return count;\n",
    "}\n",
    "\n",
    "// 7\n",
    "int reverse_number(int n) {\n",
    "    int reversed = 0;\n",
    "    int isNegative = 0;\n",
    "    \n",
    "    if (n < 0) {\n",
    "        isNegative = 1;\n",
    "        n = -n;\n",
    "    }\n",
    "    \n",
    "    while (n > 0) {\n",
    "        reversed = reversed * 10 + (n % 10);\n",
    "        n /= 10;\n",
    "    }\n",
    "    \n",
    "    if (isNegative) {\n",
    "        reversed = -reversed;\n",
    "    }\n",
    "    \n",
    "    return reversed;\n",
    "}\n",
    "\n",
    "int main() {\n",
    "    int choice, n;\n",
    "    \n",
    "    printf(\"\\033[2J\");\n",
    "    printf(\"\\033[H\");\n",
    "    printf(\"Select a task:\\n\");\n",
    "    printf(\"1 - calculate the sum \\n\");\n",
    "    printf(\"2 - count the numbers (not divisible by 3 and 7, but divisible by 5)\\n\");\n",
    "    printf(\"3 - reverse the digits of a number\\n\");\n",
    "    printf(\"Your choice: \");\n",
    "    scanf(\"%d\", &choice);\n",
    "\n",
    "    switch(choice) {\n",
    "        case 1:\n",
    "            printf(\"\\033[2J\");\n",
    "            printf(\"\\033[H\");\n",
    "            printf(\"Enter n: \");\n",
    "            scanf(\"%d\", &n);\n",
    "            if (n < 1) {\n",
    "                printf(\"Error: n must be a positive number\\n\");\n",
    "                return 1;\n",
    "            }\n",
    "            long long result1 = calculate_sum(n);\n",
    "            printf(\"Sum for n = %d: %lld\\n\", n, result1);\n",
    "            break;\n",
    "\n",
    "        case 2:\n",
    "            printf(\"\\033[2J\");\n",
    "            printf(\"\\033[H\");\n",
    "            printf(\"Enter n: \");\n",
    "            scanf(\"%d\", &n);\n",
    "            if (n < 1) {\n",
    "                printf(\"Error: n must be a positive number\\n\");\n",
    "                return 1;\n",
    "            }\n",
    "            int result2 = count_numbers(n);\n",
    "            printf(\"The number of numbers from 1 to %d is %d\\n\", n, result2);\n",
    "            printf(\"\\n\");\n",
    "            break;\n",
    "            \n",
    "        case 3:\n",
    "            printf(\"\\033[2J\");\n",
    "            printf(\"\\033[H\");\n",
    "            printf(\"Enter a number: \");\n",
    "            scanf(\"%d\", &n);\n",
    "            int result3 = reverse_number(n);\n",
    "            printf(\"The number with the reverse order of digits is %d\\n\", result3);\n",
    "            break;\n",
    "            \n",
    "        default:\n",
    "            printf(\"Error: invalid selection\\n\");\n",
    "            return 1;\n",
    "    }\n",
    "    \n",
    "    return 0;\n",
    "}\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9f9244a5",
   "metadata": {},
   "source": [
    "## Assembler code\n",
    "\n",
    "```ASM\n",
    "format ELF\n",
    "\n",
    "public _start\n",
    "\n",
    "macro syscall {\n",
    "    int 0x80\n",
    "}\n",
    "\n",
    "macro cout _data, _length {\n",
    "    push eax\n",
    "    push ebx\n",
    "    push edx\n",
    "    push ecx\n",
    "        mov eax, 4\n",
    "        mov ebx, 1\n",
    "        mov ecx, _data\n",
    "        mov edx, _length\n",
    "        syscall\n",
    "    pop ecx\n",
    "    pop edx\n",
    "    pop ebx\n",
    "    pop eax\n",
    "}\n",
    "\n",
    "macro cin _bufferInput, _length {\n",
    "    push ebx\n",
    "    push edx\n",
    "    push ecx\n",
    "        mov eax, 3\n",
    "        mov ebx, 0\n",
    "        mov ecx, _bufferInput\n",
    "        mov edx, _length\n",
    "        syscall\n",
    "\n",
    "        cmp eax, edx\n",
    "        jb .inputNormal\n",
    "\n",
    "        .flush:\n",
    "            mov eax, 3\n",
    "            mov ebx, 0\n",
    "            mov ecx, _bufferInput       ; временный буфер\n",
    "            mov edx, 1                  ; читаем по 1 символу\n",
    "            syscall\n",
    "            \n",
    "            cmp eax, 1\n",
    "            jne .flushed\n",
    "            cmp byte [ecx], 10          ; дошли до '\\n'\n",
    "            jne .flush\n",
    "\n",
    "        .flushed:\n",
    "            mov eax, -1\n",
    "            jmp invalidLengthInput\n",
    "\n",
    "        .inputNormal:\n",
    "            dec eax\n",
    "            mov edi, _bufferInput\n",
    "            add edi, eax\n",
    "            mov byte [edi], 0\n",
    "    pop ecx\n",
    "    pop edx\n",
    "    pop ebx\n",
    "}\n",
    "\n",
    "section '.function' executable\n",
    "    ; input - address char eax\n",
    "    ; output - int ecx\n",
    "    CastCharInt:\n",
    "        push ebx\n",
    "        push edi\n",
    "            ; check a minus\n",
    "            xor ebx, ebx\n",
    "            cmp byte [eax], '-'\n",
    "            sete bl ; if equal bl = 1, else bl = 0\n",
    "\n",
    "            ; skip a minus\n",
    "            cmp bl, 1\n",
    "            jne .startConvert\n",
    "            inc eax\n",
    "\n",
    "            .startConvert:\n",
    "                xor ecx, ecx\n",
    "            .cycleConvert:\n",
    "                movzx edi, byte [eax]\n",
    "                add ecx, edi\n",
    "                sub ecx, '0'\n",
    "                inc eax\n",
    "\n",
    "                cmp byte [eax], 0\n",
    "                je .cycleConvertEnd\n",
    "\n",
    "                imul ecx, 10\n",
    "                jmp .cycleConvert\n",
    "            .cycleConvertEnd:\n",
    "\n",
    "            cmp bl, 1\n",
    "            jne .return\n",
    "\n",
    "            imul ecx, -1\n",
    "\n",
    "            .return:\n",
    "        pop edi\n",
    "        pop ebx\n",
    "        ret\n",
    "    \n",
    "    ; input - int eax\n",
    "    ; output - bufferOutput\n",
    "    CastIntChar:\n",
    "    push ecx\n",
    "    push edi\n",
    "    push edx\n",
    "    push ebx\n",
    "    push esi\n",
    "        ; check a sign\n",
    "        xor ebx, ebx\n",
    "        cmp eax, 0\n",
    "        jge .positive\n",
    "        \n",
    "        ; negative\n",
    "        mov ebx, 1              ; negative flag\n",
    "        neg eax                 ; make positive\n",
    "    \n",
    "        .positive:\n",
    "            ; find number length\n",
    "            push eax\n",
    "            push ebx            ; save sign flag\n",
    "                mov ecx, 0\n",
    "                mov esi, 10\n",
    "                \n",
    "                test eax, eax   ; if zero number\n",
    "                jnz .cycleIntLen\n",
    "                mov ecx, 1      ; size 1 for zero number\n",
    "                jmp .CycleDone\n",
    "                \n",
    "                .cycleIntLen:\n",
    "                    xor edx, edx\n",
    "                    div esi\n",
    "                    inc ecx\n",
    "                    test eax, eax\n",
    "                    jnz .cycleIntLen\n",
    "                .CycleDone:\n",
    "            pop ebx             ; restore sign flag\n",
    "            pop eax\n",
    "            \n",
    "            ; consider sign in size\n",
    "            test ebx, ebx\n",
    "            jz .unsign\n",
    "            inc ecx\n",
    "            \n",
    "        .unsign:\n",
    "            push ebx            ; save sign flag\n",
    "                mov esi, 10\n",
    "                mov edi, bufferOutput\n",
    "                add edi, ecx\n",
    "                mov byte [edi], 0\n",
    "                dec edi\n",
    "                \n",
    "                .cycleConverter:\n",
    "                    xor edx, edx\n",
    "                    div esi\n",
    "                    add dl, '0'\n",
    "                    mov byte [edi], dl\n",
    "                    dec edi\n",
    "                    test eax, eax\n",
    "                    jnz .cycleConverter\n",
    "            .conversionDone:\n",
    "            pop ebx\n",
    "            \n",
    "            ; add sign\n",
    "            test ebx, ebx\n",
    "            jz .return\n",
    "            mov byte [edi], '-'\n",
    "            \n",
    "        .return:\n",
    "            mov dword [lengthOutput], ecx\n",
    "    pop esi\n",
    "    pop ebx\n",
    "    pop edx\n",
    "    pop edi\n",
    "    pop ecx\n",
    "    ret\n",
    "\n",
    "section '.data' writeable\n",
    "    messageOptionProgram:\n",
    "        db 'Choose one program:', 10\n",
    "        db '1 - compute sum using the formula', 10\n",
    "        db '2 - integers number between 1 - n are not divisible by 3 or 7, but are divisible by 5', 10\n",
    "        db '3 - get a number from the digits of the number n written in reverse order.', 10, 10\n",
    "        db 'q / Q - quite the programm', 10\n",
    "    messageOptionEnd:\n",
    "    kLengthMessageOptionProgram equ messageOptionEnd - messageOptionProgram\n",
    "\n",
    "    messageInstruction:\n",
    "        db 'Write positive number (input length 255 digits):', 10\n",
    "    messageInstructionEnd:\n",
    "    kLengthMessageInstruction = messageInstructionEnd - messageInstruction\n",
    "\n",
    "    messageInvalidNumberArgument:\n",
    "        db 0x1B, '[H', 0x1B, '[J'\n",
    "        db 'Error: write 1 or 3 additional arguments', 10, 10\n",
    "    messageInvalidOptionEnd:\n",
    "    kLengthMessageInvalidNumberArgument equ messageInvalidOptionEnd - messageInvalidNumberArgument\n",
    "\n",
    "    messageInvalidLengthInput:\n",
    "        db 0x1B, '[H', 0x1B, '[J'\n",
    "        db 'Error: input length must be 255 digits', 10, 10\n",
    "    messageInvalidLengthInputEnd:\n",
    "    kLengthMessageInvalidLengthInput equ messageInvalidLengthInputEnd - messageInvalidLengthInput\n",
    "\n",
    "    messageInvalidLengthNumber:\n",
    "        db 0x1B, '[H', 0x1B, '[J'\n",
    "        db 'Error: number must be <= 111 111 110 for this mirroring method', 10, 10\n",
    "    messageInvalidLengthNumberEnd:\n",
    "    kLengthMessageInvalidLengthNumber equ messageInvalidLengthNumberEnd - messageInvalidLengthNumber\n",
    "\n",
    "    messageResult:\n",
    "        db 'Result:'\n",
    "    messageResultEnd:\n",
    "    kLengthMessageResult equ messageResultEnd - messageResult\n",
    "\n",
    "    terminalClear db 0x1B, '[H', 0x1B, '[J'\n",
    "    newLine db 10\n",
    "\n",
    "    lengthInput dd 0\n",
    "    lengthOutput dd 0\n",
    "\n",
    "section '.bss' writeable\n",
    "    bufferInput rb 256\n",
    "    bufferOutput rb 256\n",
    "    bufferCalculation rb 4\n",
    "\n",
    "section '.error' executable\n",
    "    invalidRangeOption:\n",
    "        cout messageInvalidNumberArgument, kLengthMessageInvalidNumberArgument\n",
    "        jmp main\n",
    "\n",
    "    invalidLengthInput:\n",
    "        cout messageInvalidLengthInput, kLengthMessageInvalidLengthInput\n",
    "        jmp main\n",
    "    \n",
    "    invalidLengthNumber:\n",
    "        cout messageInvalidLengthNumber, kLengthMessageInvalidLengthNumber\n",
    "        jmp main\n",
    "\n",
    "section '.text' executable    \n",
    "_start:\n",
    "    cout terminalClear, 6\n",
    "main:\n",
    "    cout messageOptionProgram, kLengthMessageOptionProgram\n",
    "\n",
    "    cin bufferInput, 256\n",
    "    mov [lengthInput], eax\n",
    "\n",
    "    cmp dword [lengthInput], 1\n",
    "    jne invalidRangeOption\n",
    "\n",
    "    mov al, [bufferInput]\n",
    "\n",
    "    cmp al, 'q'\n",
    "    je return\n",
    "    cmp al, 'Q' \n",
    "    je return\n",
    "\n",
    "    cmp al, '1'\n",
    "    jb invalidRangeOption\n",
    "    cmp al, '3'\n",
    "    ja invalidRangeOption\n",
    "\n",
    "    cmp al, '1'\n",
    "    je executionFirst\n",
    "    cmp al, '2'\n",
    "    je executionSecond\n",
    "    cmp al, '3'\n",
    "    je executionThird\n",
    "\n",
    "    executionFirst:\n",
    "        cout terminalClear, 6\n",
    "\n",
    "        cout messageInstruction, kLengthMessageInstruction\n",
    "\n",
    "        push eax\n",
    "        push ebx\n",
    "        push esi\n",
    "        push ecx\n",
    "        push edi\n",
    "            cin bufferInput, 256\n",
    "            mov dword [lengthInput], eax\n",
    "\n",
    "            mov eax, bufferInput\n",
    "            call CastCharInt        ; int in ecx\n",
    "\n",
    "            ; make number positive\n",
    "            test ecx, ecx\n",
    "            jns .positive\n",
    "            neg ecx\n",
    "            .positive:\n",
    "\n",
    "            xor eax, eax            ; sum result\n",
    "            mov esi, 1              ; analogue k\n",
    "            .cycleSum:\n",
    "                .formula:\n",
    "                    mov edi, 1      ; formula result\n",
    "                    test esi, 1     ; check the first bit\n",
    "                    jz .evenNumber  ; if ZF=1, even number\n",
    "\n",
    "                    imul edi, -1\n",
    "\n",
    "                    .evenNumber:\n",
    "                    mov ebx, esi\n",
    "                    add ebx, 4\n",
    "                    imul ebx, esi\n",
    "                    imul edi, ebx\n",
    "\n",
    "                    mov ebx, esi\n",
    "                    add ebx, 8\n",
    "                    imul edi, ebx\n",
    "                \n",
    "                add eax, edi\n",
    "                inc esi\n",
    "                cmp esi, ecx\n",
    "                jbe .cycleSum\n",
    "\n",
    "            call CastIntChar\n",
    "        pop edi\n",
    "        pop ecx\n",
    "        pop esi\n",
    "        pop ebx\n",
    "        pop eax\n",
    "    \n",
    "        cout messageResult, kLengthMessageResult\n",
    "        cout newLine, 1\n",
    "        cout bufferOutput, [lengthOutput]\n",
    "        cout newLine, 1\n",
    "        cout newLine, 1\n",
    "\n",
    "        jmp main\n",
    "\n",
    "    executionSecond:\n",
    "        cout terminalClear, 6\n",
    "\n",
    "        cout messageInstruction, kLengthMessageInstruction\n",
    "\n",
    "        push eax\n",
    "        push ebx\n",
    "        push esi\n",
    "        push ecx\n",
    "        push edx\n",
    "            cin bufferInput, 256\n",
    "            mov dword [lengthInput], eax\n",
    "\n",
    "            mov eax, bufferInput\n",
    "            call CastCharInt        ; int in ecx\n",
    "\n",
    "            ; make number positive\n",
    "            test ecx, ecx\n",
    "            jns .positive\n",
    "            neg ecx\n",
    "            .positive:\n",
    "\n",
    "            inc ecx\n",
    "            xor esi, esi            ; similarity counter\n",
    "            .cycleCheckStatement:\n",
    "                dec ecx\n",
    "                cmp ecx, 1\n",
    "                jb .cycleCheckStatementDone\n",
    "\n",
    "                mov eax, ecx\n",
    "                xor edx, edx\n",
    "                mov ebx, 3\n",
    "                div ebx\n",
    "                test edx, edx\n",
    "                jz .cycleCheckStatement\n",
    "\n",
    "                mov eax, ecx\n",
    "                xor edx, edx\n",
    "                mov ebx, 7\n",
    "                div ebx\n",
    "                test edx, edx\n",
    "                jz .cycleCheckStatement\n",
    "\n",
    "                mov eax, ecx\n",
    "                xor edx, edx\n",
    "                mov ebx, 5\n",
    "                div ebx\n",
    "                test edx, edx\n",
    "                jnz .cycleCheckStatement\n",
    "\n",
    "                inc esi\n",
    "                jmp .cycleCheckStatement\n",
    "            .cycleCheckStatementDone:\n",
    "\n",
    "            mov eax, esi\n",
    "            call CastIntChar\n",
    "        pop edx\n",
    "        pop ecx\n",
    "        pop esi\n",
    "        pop ebx\n",
    "        pop eax\n",
    "\n",
    "        cout messageResult, kLengthMessageResult\n",
    "        cout newLine, 1\n",
    "        cout bufferOutput, [lengthOutput]\n",
    "        cout newLine, 1\n",
    "        cout newLine, 1\n",
    "\n",
    "        jmp main\n",
    "\n",
    "    executionThird:\n",
    "        cout terminalClear, 6\n",
    "\n",
    "        cout messageInstruction, kLengthMessageInstruction\n",
    "\n",
    "        push eax\n",
    "        push ebx\n",
    "        push esi\n",
    "        push ecx\n",
    "        push edx\n",
    "            cin bufferInput, 256\n",
    "            mov dword [lengthInput], eax\n",
    "\n",
    "            mov eax, bufferInput\n",
    "            call CastCharInt        ; int in ecx\n",
    "\n",
    "            ; make number positive\n",
    "            test ecx, ecx\n",
    "            jns .positive\n",
    "            neg ecx\n",
    "            .positive:\n",
    "\n",
    "            cmp ecx, 111111110\n",
    "            ja invalidLengthNumber\n",
    "\n",
    "            mov eax, ecx\n",
    "            xor ecx, ecx\n",
    "            .cycleMirror:\n",
    "                imul ecx, 10\n",
    "                xor edx, edx\n",
    "                mov ebx, 10\n",
    "                div ebx\n",
    "                add ecx, edx\n",
    "                test eax, eax\n",
    "                jnz .cycleMirror\n",
    "            \n",
    "            mov eax, ecx\n",
    "            call CastIntChar\n",
    "        pop edx\n",
    "        pop ecx\n",
    "        pop esi\n",
    "        pop ebx\n",
    "        pop eax\n",
    "\n",
    "        cout messageResult, kLengthMessageResult\n",
    "        cout newLine, 1\n",
    "        cout bufferOutput, [lengthOutput]\n",
    "        cout newLine, 1\n",
    "        cout newLine, 1\n",
    "\n",
    "        jmp main\n",
    "\n",
    "    return:\n",
    "        mov eax, 1\n",
    "        xor ebx, ebx\n",
    "        syscall\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
